plugins {
    id 'java'

    id 'checkstyle'
    id 'com.diffplug.spotless' version '8.1.0'
}


repositories {
    mavenCentral()
}


dependencies {
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.2'
    // https://mvnrepository.com/artifact/org.junit.platform/junit-platform-launcher
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.1'

    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation 'org.mockito:mockito-core:5.20.0'
}


java.toolchain.languageVersion = JavaLanguageVersion.of(25)  // use current Long-Term Support (LTS) release
java.sourceCompatibility = JavaVersion.VERSION_25
java.targetCompatibility = JavaVersion.VERSION_25

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = ['src/main/java/', 'solution/main/java/']
sourceSets.test.java.srcDirs = ['src/test/java/']


test {
    useJUnitPlatform()
}

checkstyle {
    configFile = file('checkstyle.xml')
    // Default version vs. current version? The default version is quite old (9.3 from
    // Jan 30, 2022), so let's go with the current version (10.23.0).
    // However, this needs to be updated manually as Dependabot won't deal with this!
    toolVersion = '10.23.0'
    ignoreFailures = false
    maxWarnings = 0
}

spotless {
    java {
        googleJavaFormat().aosp().reflowLongStrings()
    }
}
